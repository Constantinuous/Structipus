
/*
 * This is a Gradle build file:
 * - Gradle Homepage: http://gradle.org/
 * - Gradle Documentation: http://gradle.org/documentation
 * - View tasks for this project: $ gradlew tasks
 */

group 'de.constantinuous'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:1.1.1")
    }
}

allprojects {
    apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'

    // apply plugin: 'checkstyle'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea{
        module{
            testSourceDirs += file('src/integrationTest/java')
        }
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}


subprojects {
    // Configurations
    configurations {
        provided
        provided.extendsFrom(compile)

        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    repositories {
        mavenCentral()
    }

    // SourceSets
    sourceSets {
        // Additional source set for integrationtests
        integrationTest {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.11'
        compile group: 'org.controlsfx', name: 'controlsfx', version: '8.40.9'
        compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.3'
        compile group: 'com.github.javaparser', name:'javaparser-core', version: '2.3.0'

        testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
        testCompile group: 'junit', name: 'junit-dep', version: '4.11'
        testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.0.38-beta'
        testCompile 'org.jbehave:jbehave-core:4.0.4'
        testCompile 'de.codecentric:jbehave-junit-runner:1.2.0'
        testCompile 'net.serenity-bdd:serenity-core:1.1.24'
        testCompile 'net.serenity-bdd:serenity-jbehave:1.5.0'

        integrationTestCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
        integrationTestCompile group: 'junit', name: 'junit-dep', version: '4.11'
        integrationTestCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
        integrationTestCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.3'
    }

    task integrationTest(type: Test) {
        description = 'Runs Integration Tests'
        //testLogging.showStandardStreams = true

        reports {
            junitXml.destination "${testResultsDir}/integrationTest"
            html.destination "${reporting.baseDir}/integrationTest"
        }

        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
    }
}


project(':application') {
    apply plugin: 'net.serenity-bdd.aggregator'
}
